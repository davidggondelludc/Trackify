plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "androidx.navigation.safeargs.kotlin"
    id "dagger.hilt.android.plugin"
    id "com.google.android.libraries.mapsplatform.secrets-gradle-plugin"
    id "com.google.gms.google-services"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.apm.trackify"
        minSdk 21
        targetSdk 32

        // This number is used only to determine whether one version is more recent than another,
        // with higher numbers indicating more recent versions
        versionCode 2

        // A string used as the version number shown to users. The value is a string so that you
        // can describe the app version as a <major>.<minor>.<point> string
        versionName "0.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            // Enables code shrinking, obfuscation and optimization for release build type
            minifyEnabled true
            shrinkResources true

            // Includes the default ProGuard rules files that are packaged with the Android Gradle
            // plugin
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }
}

ext {
    splashscreen_version = "1.0.0-beta02"

    core_version = "1.7.0"
    activity_version = "1.4.0"
    fragment_version = "1.4.1"
    lifecycle_version = "2.4.1"

    appcompat_version = "1.4.1"
    constraintlayout_version = "2.1.3"
    coordinatorlayout_version = "1.2.0"
    recyclerview_version = "1.2.1"

    // Material 1.5.0 has bugs with components not rendering on preview mode
    // https://github.com/material-components/material-components-android/issues/2457
    material_version = "1.4.0"

    okhttp3_version = "4.9.0"
    retrofit_version = "2.9.0"
    glide_version = "4.13.1"

    spotify_version = "1.2.5"
    firebase_version = "29.3.1"
    firestore_version = "24.1.1"

    maps_version = "18.0.2"
    location_version = "19.0.1"
    coroutines_version = "1.6.1"

    junit_version = "4.13.2"
    assertions_version = "1.1.3"
    espresso_version = "3.4.0"
}

dependencies {
    implementation "androidx.core:core-splashscreen:$splashscreen_version"

    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$coordinatorlayout_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"

    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"

    //noinspection GradleDependency
    implementation "com.google.android.material:material:$material_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    //noinspection GradleDependency
    implementation "com.spotify.android:auth:$spotify_version"

    implementation platform("com.google.firebase:firebase-bom:$firebase_version")
    implementation "com.google.firebase:firebase-auth-ktx"
    implementation "com.google.firebase:firebase-firestore:$firestore_version"

    implementation "com.google.android.gms:play-services-maps:$maps_version"
    implementation "com.google.android.gms:play-services-location:$location_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutines_version"

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$assertions_version"

    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
}
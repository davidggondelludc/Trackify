plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.apm.trackify"
        minSdk 24
        targetSdk 32

        /**
         * This number is used only to determine whether one version is more recent than another,
         * with higher numbers indicating more recent versions.
         */
        versionCode 1

        /**
         * A string used as the version number shown to users. The value is a string so that you
         * can describe the app version as a <major>.<minor>.<point> string.
         */
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            /**
             * Enables code shrinking, obfuscation and optimization for release build type.
             * Resource shrinking is performed by the Android Gradle plugin.
             */
            minifyEnabled true
            shrinkResources true

            /**
             * Includes the default ProGuard rules files that are packaged with the Android Gradle
             * plugin.
             */
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }

    viewBinding {
        enabled = true
    }
}

ext {
    /**
     * Appcompat: allows access to new APIs on older API versions of the platform.
     */
    appcompat = "1.4.1"

    /**
     * Core: target the latest platform features and APIs while also supporting older devices.
     */
    core = "1.7.0"

    /**
     * ConstraintLayout: position and size widgets in a flexible way with relative positioning.
     */
    constraintlayout = "2.1.3"

    /**
     * Espresso: UI testing for Android.
     */
    espresso = "3.4.0"

    /**
     * Glide: fast and efficient media management and image loading that wraps media decoding,
     * memory, disk caching and resource pooling.
     */
    glide = "4.13.1"

    /**
     * jUnit: unit testing framework for Java.
     */
    junit = "4.13.2"
    junitassertions = "1.1.3"

    /**
     * Lifecycle: lifecycle-aware components perform actions in response to a change in the
     * lifecycle status of another component, such as activities and fragments.
     */
    lifecycle = "2.4.1"

    /**
     * Material: material components for Android.
     */
    material = "1.5.0"

    /**
     * Google Maps: it offers satellite imagery, aerial photography, street maps, 360Â° interactive
     * panoramic views of streets, real-time traffic conditions and route planning for traveling by
     * foot, car, bike, air and public transportation.
     */
    maps = "18.0.2"

    /**
     * Navigation: navigating between destinations within the application whether destinations are
     * implemented as Fragments, Activities or other components.
     */
    navigation = "2.4.1"

    /**
     * RecyclerView: display large sets of data in the UI while minimizing memory usage.
     */
    recyclerview = "1.2.1"
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.core:core-ktx:$core"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"
    implementation "androidx.recyclerview:recyclerview:$recyclerview"
    implementation "com.github.bumptech.glide:glide:$glide"
    implementation "com.google.android.gms:play-services-maps:$maps"
    implementation "com.google.android.material:material:$material"

    testImplementation "junit:junit:$junit"

    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.test.ext:junit:$junitassertions"
}